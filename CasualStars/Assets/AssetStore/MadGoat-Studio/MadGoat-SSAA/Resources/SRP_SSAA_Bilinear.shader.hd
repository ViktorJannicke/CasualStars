Shader "Hidden/SSAA_Bilinear_HDRP"
{
    Properties
    {
        [HideInInspector]_MainTex("Base (RGB)", 2D) = "white" {}
    }
 
    HLSLINCLUDE
 
#include "Packages/com.unity.render-pipelines.core/ShaderLibrary/Common.hlsl"
#include "UnityShaderVariables.cginc"

  float2 FixVFlip(float2 texcoord)
    {
#if UNITY_UV_STARTS_AT_TOP
        bool flip = _ProjectionParams.x > 0;
#else
        bool flip = _ProjectionParams.x < 0;
#endif
        if (flip) texcoord.y = 1 - texcoord.y;
        return texcoord;
    }
    TEXTURE2D(_MainTex);
    SAMPLER(sampler_MainTex);
 
	float _ResizeHeight;
	float _ResizeWidth;
	float _Sharpness;
	float _SampleDistance;

    // Vertex shader (procedural fullscreen triangle)
    void Vertex(
        uint vertexID : SV_VertexID,
        out float4 positionCS : SV_POSITION,
        out float2 texcoord : TEXCOORD0
    )
    {
        positionCS = GetFullScreenTriangleVertexPosition(vertexID);
        texcoord = FixVFlip(GetFullScreenTriangleTexCoord(vertexID));
    }
 
    // Fragment shader
    float4 Fragment(
        float4 positionCS : SV_POSITION,
        float2 texcoord : TEXCOORD0
    ) : SV_Target
    {
		float squareW = (_SampleDistance / _ResizeWidth);
		float squareH = (_SampleDistance / _ResizeHeight);

		// neighbor pixels
		float4 top = SAMPLE_TEXTURE2D(_MainTex, sampler_MainTex, texcoord + float2(0.0f, -squareH));
		float4 left = SAMPLE_TEXTURE2D(_MainTex, sampler_MainTex, texcoord + float2(-squareW, 0.0f));
		float4 mid = SAMPLE_TEXTURE2D(_MainTex, sampler_MainTex, texcoord + float2(0.0f, 0.0f));
		float4 right = SAMPLE_TEXTURE2D(_MainTex, sampler_MainTex, texcoord + float2(squareW, 0.0f));
		float4 bot = SAMPLE_TEXTURE2D(_MainTex, sampler_MainTex, texcoord + float2(0.0f, squareH));
        
		// just output the texture
		// avg
		float4 sampleaverage = (top + left + right + bot) / 4;
		return lerp(sampleaverage, mid, _Sharpness);

    }
    ENDHLSL
 
    SubShader
    {
        Cull Off ZWrite Off ZTest Always
        Pass
        {
            HLSLPROGRAM
            #pragma vertex Vertex
            #pragma fragment Fragment
            ENDHLSL
        }
    }
}